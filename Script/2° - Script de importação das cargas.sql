/*
* AUTOR: DIOGO FERREIRA; GUSTAVO GUIMARÃES
* DATA : 07/03/2022
* DESC : SCRIPT DE IMPORTAÇÃO DE CARGA.
* OBS  : COLOCAR O ARQUIVO DE CARCA NO DIRETÓRIO C:\
*		 DEFINIR A ULTIMA LINHA A SER IMPORTADA (UMA LINHA ANTES DO FOOTER)
*/

-----------------------HABILITAR AMBIENTE PARA EXECUÇÃO DOS COMANDOS(EXECUTAR APENAS UMA VEZ A CONFIGURAÇÃO)-----------------------------
EXEC sp_configure 'show advanced options', 1
GO
RECONFIGURE
GO
EXEC sp_configure 'xp_cmdshell', 1
GO
RECONFIGURE
GO
-----------------------------------------------------------------------------------------------------------------------------------------


IF OBJECT_ID('FILTROS.DBO.TB_TEMP') IS NULL ----VERIFICA SE A TABELA NÃO EXISTE
BEGIN
	CREATE TABLE FILTROS.DBO.TB_TEMP ( LINHA VARCHAR(MAX)) 
END

BULK INSERT FILTROS.DBO.TB_TEMP
FROM 'C:\COTAHIST_A2021.TXT' ----DIRETORIO DO ARQUIVO
WITH
    (  
		CODEPAGE = 'ACP',----CODIFICAÇÃO PARA ACEITAR ACENTUAÇÃO
		FIRSTROW = 2, ----PRIMEIRA LINHA DE DADOS (DESCONSIDERAR O TITULO/NOME DOS CAMPOS)
		LASTROW = 1831863, -----DEFINIRA A ULTIMA LINHA
        --FIELDTERMINATOR =';',----DELIMITADOR DE CAMPO, INFORMA ONDE ACABA UM CAMPO
		ROWTERMINATOR ='\n' ----DELIMITADOR DE LINHA, INFORMA ONDE ACABA A LINHA
    ) ;

GO

IF OBJECT_ID('FILTROS.DBO.TB_COTA') IS NULL
BEGIN
	CREATE TABLE FILTROS.DBO.TB_COTA (
		IDCOTA_COT INT IDENTITY(1,1) PRIMARY KEY,
		TIPREG_COT INT,
		DTPREG_COT DATETIME,
		CODBDI_COT INT,
		CODNEG_COT VARCHAR(20),
		TPRMER_COT INT,
		NOMRES_COT VARCHAR(50),
		ESPECI_COT VARCHAR(20),
		PRAZOT_COT VARCHAR(MAX),
		MODREF_COT VARCHAR(10),
		PREABE_COT MONEY,
		PREMAX_COT MONEY,
		PREMIN_COT MONEY,
		PREMED_COT MONEY,
		PREULT_COT MONEY,
		PREOFC_COT MONEY,
		PREOFV_COT MONEY,
		TOTNEG_COT BIGINT,
		QUATOT_COT BIGINT,
		VOLTOT_COT BIGINT,
		PREEXE_COT MONEY,
		INDOPC_COT BIGINT,
		DATVEN_COT DATETIME,
		FATCOT_COT BIGINT,
		PTOEXE_COT FLOAT,
		CODISI_COT VARCHAR(MAX),
		DISMES_COT BIGINT		


	)
END
GO


INSERT INTO FILTROS.DBO.TB_COTA(
	[TIPREG_COT],
	[DTPREG_COT],
	[CODBDI_COT],
	[CODNEG_COT],
	[TPRMER_COT],
	[NOMRES_COT],
	[ESPECI_COT],
	[PRAZOT_COT],
	[MODREF_COT],
	[PREABE_COT],
	[PREMAX_COT],
	[PREMIN_COT],
	[PREMED_COT],
	[PREULT_COT],
	[PREOFC_COT],
	[PREOFV_COT],
	[TOTNEG_COT],
	[QUATOT_COT],
	[VOLTOT_COT],
	[PREEXE_COT],
	[INDOPC_COT],
	[DATVEN_COT],
	[FATCOT_COT],
	[PTOEXE_COT],
	[CODISI_COT],
	[DISMES_COT]	
)
SELECT DISTINCT 
CONVERT(INT,SUBSTRING(LINHA,1,2)) AS [TIPREG_COT],
CONVERT(DATETIME,SUBSTRING(LINHA,3,8),101) AS [DTPREG_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,11,2)) AS [CODBDI_COT],
SUBSTRING(LINHA,13,12) AS [CODNEG_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,25,3)) AS [TPRMER_COT],
SUBSTRING(LINHA,28,12) AS [NOMRES_COT],
SUBSTRING(LINHA,40,10) AS [ESPECI_COT],
SUBSTRING(LINHA,50,3) AS [PRAZOT_COT],
SUBSTRING(LINHA,53,4) AS [MODREF_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,57,10),'.',SUBSTRING(LINHA,67,2))) AS [PREABE_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,70,10),'.',SUBSTRING(LINHA,80,2))) AS [PREMAX_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,83,10),'.',SUBSTRING(LINHA,93,2))) AS [PREMIN_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,96,10),'.',SUBSTRING(LINHA,106,2))) AS [PREMED_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,109,10),'.',SUBSTRING(LINHA,119,2))) AS [PREULT_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,122,12),'.',SUBSTRING(LINHA,132,2))) AS [PREOFC_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,135,10),'.',SUBSTRING(LINHA,145,2))) AS [PREOFV_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,148,5)) AS [TOTNEG_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,153,18))  AS [QUATOT_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,171,17))  AS [VOLTOT_COT],
CONVERT(MONEY,CONCAT(SUBSTRING(LINHA,189,10),'.',SUBSTRING(LINHA,199,2))) AS [PREEXE_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,202,1)) AS [INDOPC_COT],
CONVERT(DATETIME,SUBSTRING(LINHA,203,8),101) AS [DATVEN_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,211,7)) AS [FATCOT_COT],
CONVERT(FLOAT,CONCAT(SUBSTRING(LINHA,218,10),'.',SUBSTRING(LINHA,228,2))) AS [PTOEXE_COT],
SUBSTRING(LINHA,231,12) AS [CODISI_COT],
CONVERT(BIGINT,SUBSTRING(LINHA,243,3)) AS [DISMES_COT]
FROM FILTROS.DBO.TB_TEMP(NOLOCK)
GO

----DELETA TABLE TEMPORARIA SE EXISTIR
IF OBJECT_ID('FILTROS.DBO.TB_TEMP') IS NOT NULL ----VERIFICA SE A TABELA NÃO EXISTE
BEGIN
	DROP TABLE FILTROS.DBO.TB_TEMP
END
GO
------EXIBE A TABELA DEFINITIVA AO FINAL DE TUDO
--SELECT *
--FROM FILTROS.DBO.TB_COTA WITH(NOLOCK)








